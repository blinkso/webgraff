plugins {
    id 'java'
    id 'idea'
    id 'jacoco'
    id 'org.jetbrains.kotlin.jvm' version '1.5.10'
    id 'org.jetbrains.kotlin.kapt' version '1.5.10'
    id 'org.jetbrains.kotlin.plugin.spring' version '1.5.10'
    id 'org.springframework.boot' version '2.3.5.RELEASE'
}

ext {
    kotlinVersion = '1.5.10'
    springBootVersion = '2.3.5.RELEASE'
}

allprojects {
    group = 'me.ruslanys.telegraff'
    version = '1.0.0'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'maven'
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'jacoco'
    apply plugin: 'kotlin'
    apply plugin: 'kotlin-spring'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = JavaVersion.VERSION_1_8

    dependencies {
        // Kotlin
        compile("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
        compile("org.jetbrains.kotlin:kotlin-reflect")
        compile("org.jetbrains.kotlin:kotlin-script-util")
        compile("org.jetbrains.kotlin:kotlin-compiler-embeddable")
        compile("org.jetbrains.kotlin:kotlin-scripting-compiler-embeddable")

        compile("net.java.dev.jna:jna:5.11.0")

        // Tests
        testCompile("org.springframework.boot:spring-boot-starter-test")
        testCompile("org.jetbrains.kotlin:kotlin-test")
    }

    dependencyManagement {
        imports { mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}") }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        main.kotlin.srcDirs += 'src/main/kotlin'
        test.kotlin.srcDirs += 'src/test/kotlin'
    }

    // Jacoco
    jacocoTestReport {
        reports {
            xml.enabled = true
            html.enabled = true
        }
    }
    check.dependsOn jacocoTestReport

    // IDEA
    idea {
        module {
            def kaptMain = file('build/generated/source/kapt/main')
            sourceDirs += kaptMain
            generatedSourceDirs += kaptMain
        }
    }

    // Kotlin
    compileKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = "1.8"
        }
    }
    compileTestKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = "1.8"
        }
    }

    // Jar
    jar {
        enabled = true
        manifest {
            attributes("Implementation-Version": version)
        }
        archiveName = rootProject.name + ".jar"
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourcesJar
    }

    bootJar {
        enabled = false
    }
}

bootJar {
    enabled = false
}